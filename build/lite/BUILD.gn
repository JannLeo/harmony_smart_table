# Copyright (c) 2020 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/lite/toolchain/${ohos_build_compiler}.gni")
import("//build/lite/ndk/ndk.gni")

group("ohos") {
    deps = []
    product_cfg = read_file("//build/lite/product/${product}.json", "json")
    foreach(subsystem, product_cfg.subsystem) {
        foreach(component, subsystem.component) {
            deps += [ component.dir ]
        }
    }
}

group("ndk") {
    # Add native API targets
    deps = []
    if (ohos_build_ndk) {
        deps += [
            "//build/lite/ndk:ndk"
        ]
    }
}

action("gen_rootfs") {
    deps = [ ":ohos" ]

    script = "//build/lite/gen_rootfs.py"
    outputs = [ "$target_gen_dir/gen_rootfs.log" ]
    out_dir = rebase_path(root_build_dir, ".")

    compiler_dir = ohos_build_compiler_dir + "/bin/"

    ohos_build_compiler_so_strip = compiler_dir + ohos_build_compiler_so_strip
    ohos_build_compiler_bin_strip = compiler_dir + ohos_build_compiler_bin_strip

    args = [
        "--path=$out_dir",
        "--board=$board_name",
        "--kernel=$ohos_kernel_type",
        "--compile_so=$ohos_build_compiler_so_strip",
        "--compile_bin=$ohos_build_compiler_bin_strip"
    ]
}
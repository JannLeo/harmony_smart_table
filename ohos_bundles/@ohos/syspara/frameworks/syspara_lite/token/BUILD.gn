#
# Copyright (c) 2020 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
import("//build/lite/config/component/lite_component.gni")
import("//build/lite/ndk/ndk.gni")

if (ohos_kernel_type == "liteos_a" || ohos_kernel_type == "linux") {
    shared_library("token_shared") {
        cflags = ["-Wall"]
        include_dirs = [
            "//base/startup/interfaces/kits/syspara_lite",
            "//base/startup/services/init_lite/include",
            "//utils/native/lite/include",
            "//base/startup/hals/syspara_lite"
            ]

        sources = ["src/token_impl_posix/token.c",]

        public_deps = [
            "//base/hiviewdfx/frameworks/hilog_lite/featured:hilog_shared",
            "//vendor/huawei/camera/hals/utils/token:haltoken_shared"
        ]
    }
}

if (ohos_kernel_type == "liteos_riscv") {
    static_library("token_static") {
        sources = [
            "src/token_impl_hal/token.c",
        ]

        include_dirs = [
            "//base/startup/interfaces/kits/syspara_lite",
            "//utils/native/lite/include",
            "//base/hiviewdfx/interfaces/kits/hilog_lite",
            "//base/startup/hals/syspara_lite"
        ]

        deps = ["//vendor/huawei/wifi-iot/hals/utils/token:hal_token_static"]
    }
}

lite_component("token") {
    features = []
    if (ohos_kernel_type == "liteos_a" || ohos_kernel_type == "linux") {
        features += [":token_shared"]
    }
    if (ohos_kernel_type == "liteos_riscv") {
        features += [":token_static"]
    }
}

ndk_lib("token_notes") {
    if (ohos_kernel_type != "liteos_riscv") {
        lib_extension = ".so"
    }
    deps = [ ]
    if (ohos_kernel_type != "liteos_riscv") {
        deps += [
            "//base/startup/frameworks/syspara_lite/token:token_shared",
        ]
    }
    head_files = [
        "//base/startup/interfaces/kits/syspara_lite",
    ]
}